RM {
  Model = name Contained<Schema*>
  Schema = name Contained<Table*>

  Table = name AttributeList? Dependency*

  AttributeList = Contained<ListOf<Attribute, ",">>
  Attribute = name Type?

  Type = List
       | Set
       | SQLType

  List = SquareContained<ListOf<Value, ",">>
  Set = Contained<ListOf<Value, ",">>
  Value = digit+
        | IContained<"'", name, "'">

  SQLType = "bigint"
          | "text"

  Dependency = arity? dependency_glyph arity? Reference

  Reference = SchemaTableName
            | TableName

  SchemaTableName = name "." name
  TableName = name

  Contained<element> = IContained<"{", element, "}">
  SquareContained<element> = IContained<"[", element, "]">

  IContained<open, element, close> = open element close

  name = first_character additional_character*
  first_character = letter | "_"
  additional_character = first_character | alnum

  arity = "*" | "+" | digit+
  dependency_glyph = "->"
}